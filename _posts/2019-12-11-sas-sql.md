 

 

 

 

## SAS SQL简介


 

 

## 1.      SQL过程步语句

SQL过程步的特征

SQL过程步并不需要对每一个查询进行重复、每条语句都是单独处理、不需要print过程步就能打印出查询结果、也不用sort过程步进行排序、不需要run、要quit来结束SQL过程步

 

SELECT：查询数据表中的数据

ALTER：增加、删除或修改数据表的列

CREATE：创建一个数据表

DELETE：删除数据表中的列

DESCRIBE：列出数据表的属性

DROP：删除数据表、视图或索引

INSERT：对数据表插入数据

SELECT：选择列进行打印

UPDATE：对已存在的数据集的列的值进行修改 

## 2.      SQL基本查询功能 

### 2.1          SELECT语句基本语法介绍

SELECT <DISTINCT> object-item <, ...object-item>

 FROM from-list 

 <WHERE sql-expression> 

 <GROUP BY group-by-item <, ... group-by-item>> 

 <HAVING sql-expression> 

 <ORDER BY order-by-item <, ... order-by-item>>; 

这里SELECT：指定被选择的列

FROM：指定被查询的表名

WHERE：子数据集的条件

GROUP BY：将数据集通过group进行分类

HAVING：根据GROUP BY的变量得到数据子集

ORDER BY：对数据集进行排序

### 2.2          查询列

我们可以像2.3那样查询指定列，也可以用*来查询所有列。例：

proc sql;

​       select *

​       from sashelp.shoes;

quit;

 

### 2.3          消除重复值

我们可以用distinct选项来消除重复值。例如，我们要得到没有重复的所有地区的名称：

proc sql;

​    select distinct Region

​    from sashelp.shoes

quit; 

### 2.4          where子集查询

#### 2.4.1    比较运算符

先列出where语句用到的比较运算符：

LT   <    小于

GT   >    大于

EQ   =    等于

LE   <=   小于或等于

GE   >=   大于或等于

NE   ^=   不等于   <>（也可以）

例如，我们要查询sales大于100000的所有数据：

proc sql;

​    select *

​    from sashelp.shoes

​    where sales>100000;

quit; 

#### 2.4.2    in

只要满足in里的任意一个值，表达式即为真，例如，我们要选择Region在Africa和Eastern Europe的所有数据：

proc sql;

​    select *

​    from sashelp.shoes

​    where Region in ('Africa','Eastern Europe');

quit;

 

#### 2.4.3    逻辑运算符

OR     |      或

AND   &     是

NOT   ^      非

例如，选择Region在Africa和Eastern Europe，且销售额大于100000的所有数据：

proc sql;

​    select *

​    from sashelp.shoes

​    where Region in ('Africa','Eastern Europe') and  sales>100000;

quit;

#### 2.4.4    CONTAINS或?：

判断某列是否包含指定字符串

例如，选择列Region包含’Afr’的数据：

**proc** **sql** outobs=**11136**;

select aeout from adsd.adae

where aeout ? '缓解';

quit;

 

#### 2.4.5    IS NULL或IS MISSING：

判断某列数据是否为空

例如，如果找出Region为空的数据：

proc sql;

​    select *

​    from sashelp.shoes

​    where Region is missing;

quit;

注意，这里我们还可以用以下表达式对where语句进行替换。如果region为数值型变量，则可以用region=.，如果region为字符型变量，则可以用region= ‘’进行替换。

 

#### 2.4.6    Between and：选择某一区间的数据

例如选择sales大于100000，但小于200000的所有数据：

proc sql;

​    select *

​    from sashelp.shoes

​    where sales between 100000 and 200000;

quit;

 

#### 2.4.7    like：判断是否能匹配某些字符

例如，选择以region以A开头的所有地区

proc sql;

​    select *

​    from sashelp.shoes

​    where Region like 'A%';

quit;

这里注意有两类通配符，‘%’可以通配任意个任意字符，‘_’只能通配一个任意字符 

#### 2.4.8    =*：类似匹配

这里由于sashelp.shoes里没有符合要求的数据，所有就用书上的例子说明一下吧：

Where lastname=* ‘smith’，出来的结果可能是：smith，smythe等 

#### 2.4.9    表达式

我们可以通过已有的列进行计算来得到新的列，这时用关键词as来给新的列赋列名，例如：

```sas
proc sql;
  select Region, Product,Sales,Stores,Sales/Stores as salesperstores
   from sashelp.shoes
quit;
```





这时结果就会多一列salesperstores，用来得到该地区该产品每个商店的平均销售量。这里要注意的是，在创建表达式时，我们还可以在SQL里用到SAS中的除LAG和DIFF之外的所有函数。

 

这里我们还可以用表达式计算出来的结果来进行子集查询，但一定要记住用calculated关键词。例如我们要找出商店平均销售量大于5000的数据：

方法一：

proc sql;

​    select Region, Product,Sales,Stores,Sales/Stores as salesperstores

​    from sashelp.shoes

​    where Sales/Stores>5000;

quit;

方法二：

proc sql;

​    select Region, Product,Sales,Stores,Sales/Stores as salesperstores

​    from sashelp.shoes

​    where calculated salesperstores>5000;

quit;

 

### 2.5          查询结果展示

#### 2.5.1    order by数据排序

默认的排序方式是升序，我们可以用DESC关键词来进行降序排列。例如以sales降序排列数据：

proc sql;

​    select *

​    from sashelp.shoes

​    order by Sales DESC;

quit;

这里提示一下，我们可以用任意多列进行排序，包括表达式结果（不用calculated），但最好是选择的列。

 

#### 2.5.2    LABEL与FORMAT

LABEL：改变输出变量名的内容

FORMAT：改变列的值的输出方式

例如，改变salesperstores的label和format

proc sql;

​    select Region, Product,Sales,Stores,

​    Sales/Stores as salesperstores

​    label='sales per stores'

​    format=dollar12.2

​    from sashelp.shoes;

quit;

 

#### 2.5.3    处理SQL常用函数

MEAN或AVG：均值

COUNT或N或FREQ：非缺失值个数

MAX：最大值

MIN：最小值

NMISS：缺失值个数

STD：标准差

SUM：求和

VAR：方差

 

##### 2.5.3.1         求和sum

proc sql;

​    select Region, Product,Sales,Stores,

​    sum(Sales,Inventory,Returns) as total

​    from sashelp.shoes;

quit;

 

##### 2.5.3.2         求均值avg

proc sql;

​    select Region, Product,Sales,Stores,

​    avg(Sales) as salesavg

​    from sashelp.shoes;

quit;

 

##### 2.5.3.3         分组求均值group by

proc sql;

​    select Region,

​    avg(Sales) as salesavg

​    from sashelp.shoes

​    group by Region;

quit;

 

##### 2.5.3.4         计数count

proc sql;

​    select Region,count(*) as count

​    from sashelp.shoes

​    group by Region;

quit;

 

##### 2.5.3.5         HAVING数据子集

proc sql;

​    select Region,count(*) as count

​    from sashelp.shoes

​    group by Region

​    having count(*)>50;

quit;

其它的就不多作介绍了，多用用就熟悉了

 

### 2.6          子查询

#### 2.6.1    找出regions平均sales大于全部平均sales的region

proc sql;

​    select Region,

​    avg(Sales) as salesavg

​    from sashelp.shoes

​    group by Region

​    having avg(Sales)>

​    (select avg(Sales) from sashelp.shoes);

quit;

 

#### 2.6.2    ANY关键词介绍

\>ANY(20,30,40)   最终效果：>20

<ANY(20,30,40)   最终效果：<40

=ANY(20,30,40)   最终效果：=20 or =30 or =40

例如，选择出region为united state的sales小于任意region为africa的sales的数据

proc sql;

​    select Region,Sales

​    from sashelp.shoes

​    where Region='United States'

​    and Sales<any

​    (select Sales from sashelp.shoes where Region='Africa');

quit;

这个例子没有多少意义，只是说明一下any的用法

 

#### 2.6.3    ALL关键词介绍

\>ALL (20,30,40)   最终效果：>40

<ALL (20,30,40)   最终效果：<20

例如，选择出region为united state的sales小于所有region为africa的sales的数据

proc sql;

​    select Region,Sales

​    from sashelp.shoes

​    where Region='United States'

​    and Sales<all

​    (select Sales from sashelp.shoes where Region='Africa');

quit;

 

#### 2.6.4    EXISTS与NOT EXISTS

proc sql; 

   select *  

​     from proclib.payroll p  

​     where exists (select *  

​                      from proclib.staff s 

​                      where p.idnumber=s.idnum  

​                            and state='CT');

 

## 3.      表连接

1 连接joins分为内连接inner joins和外连接outer joins

内连接：仅返回匹配的数据，最多可以有32个表同时进行内连接

外连接：返回所有匹配的数据和非匹配的数据，一次只能有两个表或视图进行外连接

full outer join,返回两个表中的所有的行.

迪卡尔积：返回表内所有可能的匹配情况。例如表A有10*20的数据，表B有30*40的数据，则两个表的迪卡尔积有（10+30）*（20+40）=40*60的数据

 

### 3.1          内连接

proc sql;

​    create table innerjoins as

​    select a.*,b.*

​    from March a,Delay b

​    where a.flight=b.flight and a.date=b.date;

quit; 

### 3.2          外连接

#### 3.2.1    左连接left join

proc sql;

​    create table leftjoins as

​    select *

​    from March a

​    left join Delay b

​    on a.flight=b.flight and a.date=b.date;

quit;

 

#### 3.2.2    右连接right join

proc sql;

​    create table rightjoins as

​    select *

​    from March a

​    right join Delay b

​    on a.flight=b.flight and a.date=b.date;

quit;

 

#### 3.2.3    全连接full join

proc sql;

​    create table fulljoins as

​    select *

​    from March a

​    full join Delay b

​    on a.flight=b.flight and a.date=b.date;

quit;

 

#### 3.2.4    迪卡尔积

proc sql;

​    create table cartesian as

​    select a.*,b.*

​    from March a,Delay b;

quit;

 

## 4.      数据集SET操作

这里有四类SET操作，分别是EXCEPT、INTERSECT、UNION、OUTER UNION。

EXCEPT：得到除第二个数据集以外的所有第一个数据集里的数据

INTERSECT：得到第一个数据集和第二个数据集都有的数据

UNION：得到两个数据集所有的数据，这里如果两个数据集有相同的数据，重复数据只出现一次

OUTER UNION：得到两个数据集所有的数据。

 

还有两个关键词来修改SET操作的行为：ALL和CORRESPONDING

ALL：并不删除重复值，不能与OUTER UNION合用（注意，用ALL关键词，一种情况是你不管是否会有重复值，另一个情况是不可能出现重复值，例如有主键的数据）

CORRESPONDING：只保留两个数据集都有的字段，一般简写为CORR 

### 4.1          EXCEPT

找出所有没有延迟的航班和日期：

proc sql;

​    select flight,date

​       from March

​    except

​    select flight,date

​       from Delay;

quit;

注意：这里重复值已经被删除。如果要得到所有的包含重复值的数据，就要用到关键词ALL：

proc sql;

​    select flight,date

​       from March

​    except ALL

​    select flight,date

​       from Delay;

quit;

如果只保留两个数据集都有的字段，则用到关键词CORRESPONDING（可简写为CORR）：

proc sql;

​    select *

​       from March

​    except CORR

​    select *

​       from Delay;

quit;

 

### 4.2          INTERSECT

找出所有延迟的航班和日期：

proc sql;

​    select flight,date

​       from March

​    INTERSECT 

​    select flight,date

​       from Delay;

quit;

关键词ALL和CORRESPONDING与在EXCEPT中一样，这里不再作介绍

 

### 4.3          UNION

选择所有的数据，这里的重复值只出现一次

proc sql;

​    select flight,date

​       from March

​    UNION 

​    select flight,date

​       from Delay;

quit;

关键词ALL和CORRESPONDING与在EXCEPT中一样，这里不再作介绍

 

### 4.4          OUTER UNION

proc sql;

​    select flight,date

​       from March

​    OUTER UNION 

​    select flight,date

​       from Delay;

quit;

这里，如果我们用到关键词CORR，就可以将相同字段的数据结合到一起：

proc sql;

​    select *

​       from March

​    OUTER UNION  CORR

​    select *

​       from Delay;

quit;

 

## 5.      建表

### 5.1          建空表

#### 5.1.1    通过指定变量建空表

proc sql noprint;

   create table fib

​       (varname char(30),

​       Industry char(4),

​       begindate num format date9.  label='the beginning date',

​       enddate num format date9. label='the ending date',

​       P_1 num,

​       P_5 num,

​       P_95 num,

​       P_99 num);

quit;

 

**proc** **sql**;

create table employeesql(empid num,dept char(**20**),salary num(**20**) format **6.2** label='ab');

insert into employeesql (empid,dept,salary)

values(**1**,'it', **623.3**) 

values(**3**,'hr', **611.5**) 

values(**6**,'sales', **578.6**)

values(**4**,'ad', **2323**) 

;

**quit**;

#### 5.1.2    拷贝数据集来建空表

proc sql noprint;

   create table Delaycopy(keep=flight date)

   like Delay;

quit;

 

#### 5.1.3    查询语句中，用outobs选项来建空表

proc sql noprint outobs=0;

   create table flight114 as

   select *

   from Delay;

quit;

 

### 5.2          对数据表增加数据

主要有三种方法：

#### 5.2.1    SET语句

proc sql noprint;

insert into flight114

set flight='302' ,

​    date='07MAR94'd,

​    orig='LGA',

​    dest='WAS',

​    delaycat='No Delay',

​    destype='Domestic', 

​    delay=0;

quit;

 

#### 5.2.2    VALUES语句

proc sql noprint;

insert into flight114

values('271','07MAR94'd,'LGA','PAR','1-10 Minutes','International',4);

quit;

 

#### 5.2.3    条件查询 ：建表和导入数据同时完成

proc sql noprint;

   create table flight114 as

   select *

   from Delay

   where flight='114';

quit;

 

### 5.3          完整性约束

完整性约束的好处是保证SAS数据集的连续性和正确性，它在我们更新数据或插入新的数据时，验证新的数据是否符合该变量的约束条件。

完整性约束需要8.0以上版本，符合ANSI标准，可以在表建立时或表有数据后建立约束，但不能被用于视图，也不能用于低于8.0版本的SAS系统建立的数据集。

五大类完整性限制：NOT NULL、CHECK、UNIQUE、PRIMARY KEY、FOREIGN KEY。

NOT NULL：不许出现缺失值

CHECK：指定该变量可以输入哪些值

UNIQUE：每个值必须是唯一的，其值可以为空，但只能有一个值为空

PRIMARY KEY：主键，每个值必须是唯一且非空的

FOREIGN KEY：其它表的主键，即外键，其值为关联表的主键的值且非空

 

check例：

proc sql noprint;

   create table percent

​       (varname char(30),

​       Industry char(4),

​       begindate num format date9.  label='the beginning date',

​       enddate num format date9. label='the ending date',

​       percent num,

CONSTRAINT percent _check check

(percent le 1.0));

quit;

这时，如果我们插入的数据中，percent>1的话，将会出现错误。

 

## 6.      创建视图与索引

### 6.1          视图

视图最大的好处是它只是一个存储的查询，因此不包含任何数据，这可以减少磁盘的使用空间。其它的功能与数据库的表类似。

创建视图语法：

Create View View-name as

 Query-expression ;

例：

proc sql noprint outobs=0;

   create View  delayview as

   select *

   from Delay;

quit;

 

### 6.2          索引

语法：

Create <unique> Index index-name

On table-name (column-name,column-name);

 

.

## 7.      维护表

这里主要讲一下如何更新或删除已存在的表的数据，如何对一个表增加、减少、或改变其列的属性，如何删除表、视图和索引。

 

### 7.1          更新数据

这里主要用update来更新表的数据，语法如下

Update table-name

​       Set column-name=expression,

Set column-name=expression,…

Where expression;

注意，这里的where一定要写清楚，如果没有的话，则会更新所有的数据。

例：

proc sql noprint;

update delay

set delaycat='Delay'

where  delay=0;

quit;

 

### 7.2          条件处理

用CASE语句来实现条件处理。例如，对SASHELP.shoes根据sales的大小进行分类：

语法：

Select column…

Case <case-operand>

When when-condition then result-expression

< When when-condition then result-expression>

<else result-expression >

End;

 

例：

proc sql;

​    create table shoesrank as

​    select *,

​       (case

​       when sales>100000 then 'good'

​       else 'bad'

​       end) as rank

​    from sashelp.shoes;

quit;

 

### 7.3          删除行

语法：

Delete from table-name

Where expression;

例：

proc sql;

Delete from shoesrank

Where Region='Africa';

quit;

 

### 7.4          改变列

语法：

Alter Table table-name

​       Add column-definition,column-definition,…

​       Drop column-name,column-name,…

​       Modify column-definition,column-definition,…

 

对已存在的数据表增加一个列：例：

proc sql;

alter table shoesrank

add addcolumn num format=comma10.2,

addcolumnmore char(10);

quit;

 

从已存在的数据表中删除列，例：

proc sql;

alter table shoesrank

drop addcolumn;

quit;

 

修改某列的属性，例：

proc sql;

alter table shoesrank

modify  addcolumnmore char(20) label='modify add label';

quit;

 

### 7.5          删除表、视图、索引

语法：

Drop Table table-name,table-name,…;

Drop View View-name,View-name,…;

Drop Index index-name,index-name,…;

From table-name;

例：

proc sql;

drop table shoesrank;

quit;





## 参考文献

